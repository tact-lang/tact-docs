# Common

import { Callout } from 'nextra-theme-docs'

List of a basic functions that are available in all smart contracts.

## sender

```tact
fun sender(): Address;
```

Returns the [`Address{:tact}`][p] of the sender of the current message.

Usage example:

```tact
receive() {
  let whoSentMeMessage: Address = sender();
}
```

<Callout type="warning" emoji="⚠️">

  Behaviour is undefined for [getter functions](/book/contracts#getter-functions), as they cannot have a sender nor they can send messages.

</Callout>

<Callout>

  In order to reduce gas usage, prefer using this function over calling [`context().sender{:tact}`](#context) when you only need to know the sender of the message.

</Callout>

## require

```tact
fun require(condition: Bool, error: String);
```

Checks the `condition` and throws an exception with `error` message if the `condition` is `false{:tact}`. Does nothing otherwise.

Usage example:

```tact
// now() has to return a value greater than 1000, otherwise an error message will be thrown
require(now() > 1000, "We're in the first 1000 seconds of 1 January 1970!");
```

## now

```tact
fun now(): Int
```

Returns the current [Unix time](https://en.wikipedia.org/wiki/Unix_time).

Usage example:

```tact
let timeOffset: Int = now() + 1000; // thousand seconds from now()
```

## myBalance

```tact
fun myBalance(): Int;
```

Returns the remaining balance of the smart contract as an integer value in nanoToncoins, where nanoToncoin is the $\frac{1}{10^{9}}\mathrm{th}$ of the Toncoin.

Usage example:

```tact
let iNeedADolla: Int = myBalance();
```

<Callout type="warning" emoji="⚠️">

  Note, that [`send(){:tact}` function](/book/send) does not update the contract's balance.

</Callout>

## myAddress

```tact
fun myAddress(): Address;
```

Returns the address of the current smart contract as an [`Address{:tact}`][p].

Usage example:

```tact
let meMyselfAndI: Address = myAddress();
```

## newAddress

```tact
fun newAddress(chain: Int, hash: Int): Address;
```

Creates a new [`Address{:tact}`][p] based on the [`chain` id](https://ton-blockchain.github.io/docs/#/overviews/TON_blockchain_overview) and the [SHA-256](/language/ref/math#sha256) encoded [`hash` value](https://docs.ton.org/learn/overviews/addresses#account-id).

Usage example:

```tact
let oldTonFoundationAddr: Address =
    newAddress(0, 0x83dfd552e63729b472fcbcc8c45ebcc6691702558b68ec7527e1ba403a0f31a8);
    //         ↑  ------------------------------------------------------------------
    //         |  ↑
    //         |  sha-256 hash of contract's init package (StateInit)
    //         chain id: 0 is a workchain, -1 is a masterchain
```

<Callout type="warning" emoji="⚠️">

  This method throws an error with [exit code 136](/book/exit-codes#136) if `chain` is invalid or with [exit code 137](/book/exit-codes#137) if `chain` points to the masterchain ($-1$) without [masterchain support](/book/masterchain) enabled.

</Callout>

<Callout>

  **Useful links:**\
  [`chain` (Workchain ID) in TON Docs](https://docs.ton.org/learn/overviews/addresses#workchain-id)\
  [`hash` (Account ID) in TON Docs](https://docs.ton.org/learn/overviews/addresses#account-id)\
  [Contract's init package (`StateInit{:tact}`)](/book/expressions#initof)

</Callout>

## contractAddress

```tact
fun contractAddress(s: StateInit): Address;
```

Computes smart contract's [`Address{:tact}`][p] in a workchain $0$ based on its [`StateInit{:tact}`](/book/expressions#initof).

Usage example:

```tact
let foundMeSome: Address = contractAddress(initOf SomeContract());
```

## contractAddressExt

```tact
fun contractAddressExt(chain: Int, code: Cell, data: Cell): Address;
```

Computes smart contract's [`Address{:tact}`][p] based on the `chain` id, contract's `code` and contract's initial state `data`. Use [`initOf{:tact}`](/book/expressions#initof) expression to obtain initial `code` and initial `data` of a given contract.

Usage example:

```tact
let initPkg: StateInit = initOf SomeContract();
let hereBeDragons: Address = contractAddressExt(0, initPkg.code, initPkg.data);
```

<Callout type="warning" emoji="⚠️">

  This method throws an error with [exit code 136](/book/exit-codes#136) if `chain` is invalid or with [exit code 137](/book/exit-codes#137) if `chain` points to the masterchain ($-1$) without [masterchain support](/book/masterchain) enabled.

</Callout>

## address

```tact
fun address(s: String): Address;
```

A compile-time function that converts a [`String{:tact}`][p] with an address into the Address type.

Usage example:

```tact
contract Example {
    // Persistent state variables
    addr: Address =
        address("EQCD39VS5jcptHL8vMjEXrzGaRcCVYto7HUn4bpAOg8xqB2N"); // works at compile-time!
}
```

## emit

```tact
fun emit(body: Cell);
```

Sends a message `body` to the outer world with the purpose of logging and analyzing it later off-chain. The message does not have a recipient and is gas-efficient compared to alternatives.

Usage example:

```tact
emit("Catch me if you can".asComment()); // asComment() converts a String to a Cell
```

## cell

```tact
fun cell(bocBase64: String): Cell;
```

A compile-time function that embeds a base64-encoded [BoC](https://docs.ton.org/develop/data-formats/cell-boc#bag-of-cells) `bocBase64` as a [`Cell{:tact}`][p] into the contract.

Usage example:

```tact
// Init package for Wallet V3R1 as a base64-encoded BoC:
cell("te6cckEBAQEAYgAAwP8AIN0gggFMl7qXMO1E0NcLH+Ck8mCDCNcYINMf0x/TH/gjE7vyY+1E0NMf0x/T/9FRMrryoVFEuvKiBPkBVBBV+RDyo/gAkyDXSpbTB9QC+wDo0QGkyMsfyx/L/8ntVD++buA=");
```

<Callout>

  **Useful links:**\
  [Bag of Cells in TON Docs](https://docs.ton.org/develop/data-formats/cell-boc#bag-of-cells)

</Callout>

## ton

```tact
fun ton(value: String): Int;
```

A compile-time function that converts the given Toncoins `value` from a human-readable format [`String{:tact}`][p] to the nanoToncoins [`Int{:tact}`][int] format.

Usage example:

```tact
ton("1");           // 10^9 nanoToncoins, which is equal to one Toncoin
ton("0.1");         // 10^8 nanoToncoins, which is equal to 0.1 Toncoin
ton("0.000000001"); // 1 nanoToncoin, which is equal to 10^-9 Toncoins
```

## dump

```tact
fun dump(arg);
```

Prints the argument `arg` to the contract's debug console. Evaluated only if `debug` option is set in the [configuration file](/book/config), otherwise does nothing.

Can be applied to the following list of types and values:

* [`Int{:tact}`][int]
* [`Bool{:tact}`][bool]
* [`Builder{:tact}`][p], [`Cell{:tact}`][p] or [`Slice{:tact}`][p]
* [`String{:tact}`][p] or [`StringBuilder{:tact}`][p]
* [`map<k, v>{:tact}`](/book/maps)
* [Optionals and `null{:tact}` value](/book/optionals)
* `void`, which is implicitly returned when a function doesn't have return value defined

Usage examples:

```tact
// Int
dump(42);

// Bool
dump(true);
dump(false);

// Builder, Cell or Slice
dump(beginCell());           // Builder
dump(emptyCell());           // Cell
dump(emptyCell().asSlice()); // Slice

// String or StringBuilder
dump("Hello, my name is..."); // String
dump(beginTailString());      // StringBuilder

// Maps
let m: map<Int, Int> = emptyMap();
m.set(2 + 2, 4);
dump(m);

// Special values
dump(null);
dump(emit("msg".asComment())); // As emit() function doesn't return a value, dump() would print #DEBUG#: void.
```

<Callout>

  For this function to work, the compiler option `debug` has to be set to `true{:tact}` for the current project in the [configuration file](/book/config). Read more about debugging on the dedicated page: [Debugging](/book/debug).

</Callout>

## context

```tact
fun context(): Context;
```

Returns `Context{:tact}` [Struct](/book/structs-and-messages#structs), that consists of:

Field   | Type                  | Description
:------ | :-------------------- | :----------
bounced | [`Bool{:tact}`][bool] | [Bounced](https://ton.org/docs/learn/overviews/addresses#bounceable-vs-non-bounceable-addresses) flag of the incoming message.
sender  | [`Address{:tact}`][p] | Internal address of the sender on the TON blockchain.
value   | [`Int{:tact}`][int]   | Amount of nanoToncoins in a message.
raw     | [`Slice{:tact}`][p]   | The reminder of the message as a [`Slice{:tact}`][p].

Usage example:

```tact
let ctx: Context = context();
require(ctx.value != 68 + 1, "Invalid amount of nanoToncoins, bye!");
```

<Callout>

  Note, that if you only need to know who sent the message, use the [`sender(){:tact}`](#sender) function, as it's less gas-expensive.

</Callout>

[p]: /book/types#primitive-types
[bool]: /book/types#booleans
[int]: /book/integers
