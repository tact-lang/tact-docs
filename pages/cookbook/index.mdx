import { Callout } from 'nextra/components'

# Cookbook Overview

The core reason for creating the Tact Cookbook is to collect all the experience from Tact developers in one place so that future developers will use it. Compared to the rest of the documentation, this section is more focused on everyday tasks every Tact developer resolves during the development of smart contracts.

## Basics

### How to write a Hello World smart contract

```tact
contract HelloWorld {
    // Note, that an empty init function won't be needed starting with Tact 1.3.0,
    // see: https://github.com/tact-lang/tact/pull/167
    init() {}
    get fun greeting(): String {
        return "Hello, World!";
    }
}
```

### How to write an `if` statement in Tact

Tact supports `if...else{:tact}` statements in a similar syntax to most programming languages. The condition of the statement can be any boolean expression.

```tact
let value: Int = 9001;

if (value > 10) {
    // do something
}

if (value > 100) {
    // do something
} else {
    // do something else
}

if (value > 9000) {
    // do something
} else if (value > 500) {
    // do another thing
} else {
    // do something else
}
```

## Loops

### How to write a `repeat` loop

Please make sure the input number for the [`repeat{:tact}`](/book/statements#repeat-loop) loop statement is within the range of an $32$-bit [`Int{:tact}`](/book/integers) data type, as an exception will be thrown otherwise.

```tact
let sum: Int = 0;
let i: Int = 0;

repeat (10) {               // repeat exactly 10 times
    i += 1;
    sum += i;
}

```
<Callout>

  **Useful links:**\
  [`repeat{:tact}` loop in the Book](/book/statements#repeat-loop)\
  [Loops in Tact-By-Example](https://tact-by-example.org/04-loops)

</Callout>

### How to write a `while` loop

```tact
let i: Int = 0;

while (i < 10) {
    i += 1;
}
```

<Callout>

  **Useful links:**\
  [`while{:tact}` loop in the Book](/book/statements#while-loop)\
  [Loops in Tact-By-Example](https://tact-by-example.org/04-loops)

</Callout>


### How to write a `do...until` loop [#how-to-write-a-do-until-loop]

When we need the cycle to run at least once, we use the [`do...until{:tact}`](/book/statements#do-until-loop) loop.

```tact
let num: Int;               // A variable to store the random number

// A do until loop that repeats until num is equal to 5
do {
    num = random(0, 9);     // get a random number between 0 and 9
} until (num == 5);         // stop loop if num is equal to 5

dump("The loop is over!");
```

<Callout>

  **Useful links:**\
  [`do...until{:tact}` loop in the Book](/book/statements#do-until-loop)\
  [`random()` in Language→Reference](/language/ref/random#random)\
  [Loops in Tact-By-Example](https://tact-by-example.org/04-loops)

</Callout>

## Map

For description and basic usage see [`map<k, v>{:tact}` type in the Book](/book/maps).

### How to emulate an array using a map wrapped in a Struct

```tact
import "@stdlib/deploy"; // for Deployable trait

struct Array {
    map: map<Int as uint16, Int>; // array of Int values as a map of Ints to Ints,
                                  // with serialization of its keys to uint16 to save space
    length: Int = 0;              // length of the array, defaults to 0
}

// Compile-time constant upper bound for our map representing an array.
const MaxArraySize: Int = 5_000; // 5,000 entries max, to stay reasonably far from limits

// Extension mutation function for adding new entries to the end of the array
extends mutates fun append(self: Array, item: Int) {
    require(self.length + 1 <= MaxArraySize, "No space in the array left for new items!");

    self.map.set(self.length, item); // set the entry (key-value pair)
    self.length += 1;                // increase the length field
}

// Extension mutation function for inserting new entries at the given index
extends mutates fun insert(self: Array, item: Int, idx: Int) {
    require(self.length + 1 <= MaxArraySize, "No space in the array left for new items!");
    require(idx >= 0, "Index of the item cannot be negative!");
    require(idx < self.length, "Index is out of array bounds!");

    // Move all items from idx to the right
    let i: Int = self.length; // not a typo, as we need to start from the non-existing place
    while (i > idx) {
        // Note, that we use !! operator as we know for sure that the value would be there
        self.map.set(i, self.map.get(i - 1)!!);
        i -= 1;
    }

    // And put the new item in
    self.map.set(idx, item); // set the entry (key-value pair)
    self.length += 1;        // increase the length field
}

// Extension function for getting the value at the given index
extends fun getIdx(self: Array, idx: Int): Int {
    require(self.length > 0, "No items in the array!");
    require(idx >= 0, "Index of the item cannot be negative!");
    require(idx < self.length, "Index is out of array bounds!");

    // Note, that we use !! operator as we know for sure that the value would be there
    return self.map.get(idx)!!;
}

// Extension function for returning the last value
extends fun getLast(self: Array): Int {
    require(self.length > 0, "No items in the array!");

    // Note, that we use !! operator as we know for sure that the value would be there
    return self.map.get(self.length - 1)!!;
}

// Extension mutation function for deleting and entry at the given index and returning its value
extends mutates fun deleteIdx(self: Array, idx: Int): Int {
    require(self.length > 0, "No items in the array to delete!");
    require(idx >= 0, "Index of the item cannot be negative!");
    require(idx < self.length, "Index is out of array bounds!");

    // Remember the value, which is going to be deleted
    let memorized: Int = self.map.get(idx)!!;

    // Move all items from idx and including to the left
    let i: Int = idx;
    while (i + 1 < self.length) {
        // Note, that we use !! operator as we know for sure that the value would be there
        self.map.set(i, self.map.get(i + 1)!!);
        i += 1;
    }

    self.map.set(self.length - 1, null); // delete the last entry
    self.length -= 1;                    // decrease the length field

    return memorized;
}

// Extension mutation function for deleting the last entry and returning its value
extends fun deleteLast(self: Array): Int {
    require(self.length > 0, "No items in the array!");

    // Note, that we use !! operator as we know for sure that the value would be there
    let lastItem: Int = self.map.get(self.length - 1)!!;
    self.map.set(self.length - 1, null); // delete the entry
    self.length -= 1;                    // decrease the length field

    return lastItem;
}

// Extension function for deleting all items in the Array
extends mutates fun deleteAll(self: Array) {
    self.map = emptyMap();
    self.length = 0;
}

// Global static function for creating an empty Array
fun emptyArray(): Array {
    return Array{map: emptyMap()}; // length defaults to 0
}

// Contract, with emulating an Array using the map
contract MapAsArray with Deployable {
    // Persistent state variables
    array: Array;

    // Constructor (initialization) function of the contract
    init() {
        self.array = emptyArray();
    }

    // Internal message receiver, which responds to a String message "append"
    receive("append") {
        // Add a new item
        self.array.append(42);
    }

    // Internal message receiver, which responds to a String message "delete_5h"
    receive("delete_5th") {
        // Remove the 5th item if it exists and reply back with its value, or raise an erroor
        self.reply(self.array.deleteIdx(4).toCoinsString().asComment()); // index offset 0 + 4 gives the 5th item
    }

    // Internal message receiver, which responds to a String message "del_last"
    receive("del_last") {
        // Remove the last item and reply back with its value, or raise an error
        self.reply(self.array.deleteLast().toCoinsString().asComment());
    }

    // Internal message receiver, which responds to a String message "get_last"
    receive("get_last") {
        // Reply back with the latest item in the array if it exists, or raise an error
        self.reply(self.array.getLast().toCoinsString().asComment());
    }

    // Internal message receiver, which responds to a String message "delete_all"
    receive("delete_all") {
        self.array.deleteAll();
    }
}
```

### How to emulate a stack using a map wrapped in a Struct

A [stack](https://en.wikipedia.org/wiki/Stack_(abstract_data_type)) is a collection of elements with two main operations:

* push, which adds an element to the end of the collection
* pop, which removes the most recently added element

```tact
import "@stdlib/deploy"; // for Deployable trait

struct Stack {
    map: map<Int as uint16, Int>; // stack of Int values as a map of Ints to Ints,
                                  // with serialization of its keys to uint16 to save space
    length: Int = 0;              // length of the stack, defaults to 0
}

// Compile-time constant upper bound for our map representing an stack.
const MaxStackSize: Int = 5_000; // 5,000 entries max, to stay reasonably far from limits

// Extension mutation function for adding new entries to the end of the stack
extends mutates fun push(self: Stack, item: Int) {
    require(self.length + 1 <= MaxStackSize, "No space in the stack left for new items!");

    self.map.set(self.length, item); // set the entry (key-value pair)
    self.length += 1;                // increase the length field
}

// Extension mutation function for deleting the last entry and returning its value
extends mutates fun pop(self: Stack): Int {
    require(self.length > 0, "No items in the stack to delete!");

    // Note, that we use !! operator as we know for sure that the value would be there
    let lastItem: Int = self.map.get(self.length - 1)!!;
    self.map.set(self.length - 1, null); // delete the entry
    self.length -= 1;                    // decrease the length field

    return lastItem;
}

// Extension function for returning the last value
extends fun getLast(self: Stack): Int {
    require(self.length > 0, "No items in the stack!");

    // Note, that we use !! operator as we know for sure that the value would be there
    return self.map.get(self.length - 1)!!;
}

// Extension function for deleting all items in the Stack
extends mutates fun deleteAll(self: Stack) {
    self.map = emptyMap();
    self.length = 0;
}

// Global static function for creating an empty Stack
fun emptyStack(): Stack {
    return Stack{map: emptyMap()}; // length defaults to 0
}

contract MapAsStack with Deployable {
    // Persistent state variables
    stack: Stack; // our stack, which uses the map

    // Constructor (initialization) function of the contract
    init() {
        self.stack = emptyStack();
    }

    // Internal message receiver, which responds to a String message "push"
    receive("push") {
        // Add a new item
        self.stack.push(42);
    }

    // Internal message receiver, which responds to a String message "pop"
    receive("pop") {
        // Remove the last item and reply with it
        self.reply(self.stack.pop().toCoinsString().asComment());
    }

    // Internal message receiver, which responds to a String message "get_last"
    receive("get_last") {
        // Reply back with the latest item in the map if it exists, or raise an error
        self.reply(self.stack.getLast().toCoinsString().asComment());
    }

    // Internal message receiver, which responds to a String message "delete_all"
    receive("delete_all") {
        self.stack.deleteAll();
    }

    // Getter function for obtaining the stack
    get fun map(): map<Int, Int> {
        return self.stack.map;
    }

    // Getter function for obtaining the current length of the stack
    get fun length(): Int {
        return self.stack.length;
    }
}
```

### How to emulate a circular buffer using a map wrapped in a Struct

A [circular buffer](https://en.wikipedia.org/wiki/Circular_buffer) (circular queue, cyclic buffer or ring buffer) is a data structure, which uses a single, fixed-size [buffer](https://en.wikipedia.org/wiki/Data_buffer) as it were connected end-to-end.

```tact
import "@stdlib/deploy"; // for Deployable trait

struct CircularBuffer {
    map: map<Int as uint8, Int>; // circular buffer of Int values as a map of Ints to Ints,
                                 // with serialization of its keys to uint8 to save space
    length: Int = 0;             // length of the circular buffer, defaults to 0
    start: Int = 0;              // current index into the circular buffer, defaults to 0
}

// Compile-time constant upper bound for our map representing a circular buffer.
const MaxCircularBufferSize: Int = 5;

// Extension mutation function for putting new items to the circular buffer
extends mutates fun put(self: CircularBuffer, item: Int) {
    if (self.length < MaxCircularBufferSize) {
        self.map.set(self.length, item); // store the item
        self.length += 1;                // increase the length field
    } else {
        self.map.set(self.start, item);                        // store the item, overriding previous entry
        self.start = (self.start + 1) % MaxCircularBufferSize; // update starting position
    }
}

// Extension mutation function for getting an item from the circular buffer
extends mutates fun getIdx(self: CircularBuffer, idx: Int): Int {
    require(self.length > 0, "No items in the circular buffer!");
    require(idx >= 0, "Index of the item cannot be negative!");

    if (self.length < MaxCircularBufferSize) {
        // Note, that we use !! operator as we know for sure that the value would be there
        return self.map.get(idx % self.length)!!;
    }

    // Return the value rotating around the circular buffer, also guaranteed to be there
    return self.map.get((self.start + idx) % MaxCircularBufferSize)!!;
}

// Extension function for iterating over all items in the circular buffer and dumping them to the console
extends fun printAll(self: CircularBuffer) {
    let i: Int = self.start;
    repeat (self.length) {
        dump(self.map.get(i)!!); // !! tells the compiler this can't be null
        i = (i + 1) % MaxCircularBufferSize;
    }
}

// Extension function for deleting all items in the CircularBuffer
extends mutates fun deleteAll(self: CircularBuffer) {
    self.map = emptyMap();
    self.length = 0;
    self.start = 0;
}

// Global static function for creating an empty CircularBuffer
fun emptyCircularBuffer(): CircularBuffer {
    return CircularBuffer{map: emptyMap()}; // length and start default to 0
}

// This contract records the last 5 timestamps of when "timer" message was received
contract MapAsCircularBuffer with Deployable {
    // Persistent state variables
    cBuf: CircularBuffer; // our circular buffer, which uses a map

    // Constructor (initialization) function of the contract
    init() {
        self.cBuf = emptyCircularBuffer();
    }

    // Internal message receiver, which responds to a String message "timer"
    // and records the timestamp when it receives such message
    receive("timer") {
        let timestamp: Int = now();
        self.cBuf.put(timestamp);
    }

    // Internal message receiver, which responds to a String message "get_first"
    // and replies with the first item of the circular buffer
    receive("get_first") {
        self.reply(self.cBuf.getIdx(0).toCoinsString().asComment());
    }

    // Internal message receiver, which responds to a String message "print_all"
    receive("print_all") {
        self.cBuf.printAll();
    }

    // Internal message receiver, which responds to a String message "delete_all"
    receive("delete_all") {
        self.cBuf.deleteAll();
    }
}
```

<Callout>

  This example is adapted from [Arrays page in Tact-By-Example](https://tact-by-example.org/04-arrays).

</Callout>

## `Slice`

### How to determine if a `Slice` is empty

A [`Slice{:tact}`](/book/types#primitive-types) is considered _empty_ if it has no stored `data` **and** no stored `references`.

Use `empty(){:tact}` method to check if a [`Slice{:tact}`](/book/types#primitive-types) is empty.

```tact
// Create an empty Slice with no data and no refs
let empty_slice: Slice = emptyCell().asSlice();
// Returns `true`, because `empty_slice` doesn't have any data or refs
empty_slice.empty();

// Create a slice with some data
let slice_with_data: Slice = beginCell().
    storeUint(42, 8).
    asSlice();
// Returns `false`, because the slice has some data
slice_with_data.empty();

// Create a slice with a reference to an empty cell
let slice_with_refs: Slice  = beginCell().
    storeRef(emptyCell()).
    asSlice();
// Returns `false`, because the slice has a reference
slice_with_refs.empty();

// Create a slice with data and a reference
let slice_with_data_and_refs: Slice  = beginCell().
    storeUint(42, 8).
    storeRef(emptyCell()).
    asSlice();
// Returns `false`, because the slice has both data and reference
slice_with_data_and_refs.empty();
```

### How to determine if a `Slice` has no refs (but may have bits)

```tact
let slice_with_data: Slice = beginCell().
    storeUint(0, 1).
    asSlice(); // create a slice with data but without refs
let refsCount: Int = slice_with_data.refs(); // 0
let hasNoRefs: Bool = slice_with_data.refsEmpty(); // true
```

### How to determine if a `Slice` has no data (no bits, but may have refs)

```tact
let slice_with_data: Slice = beginCell().
    storeRef(emptyCell()).
    asSlice(); // create a slice with ref but without data
let bitsCount: Int = slice_with_data.bits(); // 0
let hasNoData: Bool = slice_with_data.dataEmpty(); // true
```

### How to determine if two Slices are equal

Direct comparison:

```tact
let firstSlice: Slice = "A".asSlice();
let secondSlice: Slice = "A".asSlice();

let areEqual: Bool = firstSlice == secondSlice;
let areNotEqual: Bool = firstSlice != secondSlice;

dump(areEqual) // true;
dump(areNotEqual) // false;
```

Note, that direct comparison via `=={:tact}` or `!={:tact}` operators implicitly uses hashes under the hood.

Explicit comparisons using `.hash(){:tact}` are also available:

```tact
fun areSlicesEqual(a: Slice, b: Slice): Bool {
    return a.hash() == b.hash();
}

let firstSlice: Slice = "A".asSlice();
let secondSlice: Slice = "A".asSlice();

let result: Bool = areSlicesEqual(firstSlice, secondSlice);
dump(result) // true;
```

<Callout>

  **Useful links:**\
  [`String.asSlice(){:tact}` in Language→Reference](/language/ref/strings#stringasslice)

</Callout>

## `Cell`

### How to determine if a `Cell` is empty

To check if there is any data in the [`Cell{:tact}`](/book/types#primitive-types), we should first convert it to the [`Slice{:tact}`](/book/types#primitive-types). If we are only interested in having bits, we should use `dataEmpty(){:tact}`, if only refs - `refsEmpty(){:tact}`. In case we want to check for the presence of any data, regardless of whether it is a bit or ref, we need to use `empty(){:tact}`.

```tact
// Create an empty cell with no data and no refs
let empty_cell: Cell = emptyCell(); // alias for beginCell().endCell()
// Present `cell` as a `slice` to parse it.
let slice: Slice = empty_cell.asSlice();
// Returns `true`, because `slice` doesn't have any data or refs
slice.empty();

// Create a cell with bits and references
let cell_with_data_and_refs: Cell = beginCell().
    storeUint(42, 8).
    storeRef(emptyCell()).
    endCell();
// Change `cell` type to slice with `begin_parse()`
let slice: Slice = cell_with_data_and_refs.asSlice();
// Returns `false`, because `slice` has both data and refs
slice.empty();
```

<Callout>

  **Useful links:**\
  [`empty(){:tact}` in Language→Reference](/language/ref/cells#sliceempty)\
  [`dataEmpty(){:tact}` in Language→Reference](/language/ref/cells#slicedataempty)\
  [`refsEmpty(){:tact}` in Language→Reference](/language/ref/cells#slicerefsempty)\
  [`emptyCell(){:tact}` in Language→Reference](/language/ref/cells#emptycell)\
  [`beginCell(){:tact}` in Language→Reference](/language/ref/cells#begincell)\
  [`endCell(){:tact}` in Language→Reference](/language/ref/cells#builderendcell)

</Callout>

### How to determine if two Cells are equal

Direct comparison:

```tact
let a: Cell = beginCell()
    .storeUint(123, 16)
    .endCell();

let b: Cell = beginCell()
    .storeUint(123, 16)
    .endCell();

let areCellsEqual: Bool = a == b; // true
let areCellsNotEqual: Bool = a != b; // false
```

Note, that direct comparison via `=={:tact}` or `!={:tact}` operators implicitly uses hashes under the hood.

Explicit comparisons using `.hash(){:tact}` are also available:

```tact
let a: Cell = beginCell()
    .storeUint(123, 16)
    .endCell();

let b: Cell = beginCell()
    .storeUint(123, 16)
    .endCell();

let areCellsEqual: Bool = a.hash() == b.hash(); // true
let areCellsNotEqual: Bool = a.hash() != b.hash(); // false
```

<Callout>

  **Useful links:**\
  [`Cell.hash(){:tact}` in Language→Reference](/language/ref/cells#cellhash)

</Callout>

## Sending messages

### How to make a basic reply

```tact
receive() {
    self.reply("Hello, World!".asComment()); // asComment converts a String to a Cell with a comment
}
```

### How to send a simple message

```tact
send(SendParameters{
    bounce: true, // default
    to: destinationAddress,
    value: ton("0.01"), // attached amount of Tons to send
    body: "Hello from Tact!".asComment() // comment (optional)
});
```

### How to send a message with the entire balance

If we need to send the whole balance of the smart contract, then we should use the `SendRemainingBalance{:tact}` send mode. Alternatively, we can use `mode: 128{:tact}`, which has the same meaning.

```tact
send(SendParameters{
    to: ctx.sender,
    value: 0,
    mode: SendRemainingBalance, // or mode: 128
    body: "Hello from Tact!".asComment() // comment (optional)
});
```

### How to send a message with the remaining value

If we want to make a reply to the same sender, we can use the mode `SendRemainingValue{:tact}` (i.e. `mode: 64{:tact}`), which carries all the remaining value of the inbound message in addition to the value initially indicated in the new message.

```tact
receive() {
    send(SendParameters{
        to: sender(),
        value: 0,
        mode: SendRemainingValue,
        body: "Hello from Tact!".asComment() // comment (optional)
    });
}
```

It's often useful to add the `SendIgnoreErrors{:tact}` flag too, in order to ignore any errors arising while processing this message during the action phaseL

```tact
receive() {
    send(SendParameters{
        to: sender(),
        value: 0,
        mode: SendRemainingValue | SendIgnoreErrors, // prefer | over + for the mode
        body: "Hello from Tact!".asComment()         // comment (optional)
    });
}
```

The latter example is identical to using a [`.reply(){:tact}` function](#how-to-make-a-basic-reply).

<Callout>

  **Useful links:**\
  ["Sending messages" in the Book](/book/send#send-message)\
  ["Message `mode`" in the Book](/book/message-mode)

</Callout>

### How to convert a `String` to an `Int`

```tact
// Dangerously casts string as slice for parsing. Use it only if you know what you are doing.
// Try to parse the string as an slice
let string: Slice = "26052021".asSlice();

// A variable to store the number
let number: Int = 0;

while (!string.empty()) {                   // A loop until slice has bytes
    let char: Int = string.loadUint(8);     // load slice bytes
    number = (number * 10) + (char - 48);   // we use ASCII table to get number
}

dump(number);
```

### How to convert an `Int` to a `String`

```tact
let number: Int = 261119911;

// Converting the [number] to String
let numberString: String = number.toString();

// Converting the [number] to Float String
// passed argument `3` is is a exponentiation parameter of expression 10^(-3) that will be used for computing float number. This parameter required to be `0 <= digits < 77`
let floatString: String = number.toFloatString(3);

// Converting the [number] as coins to human readable Strign
let coinsString: String = number.toCoinsString();

dump(numberString); // "261119911"
dump(floatString); // "261119.911"
dump(coinsString); // "0.261119911"
```

<Callout>

  **Useful links:**\
  [`Int.toString(){:tact}` in Language→Reference](/language/ref/strings#inttostring)\
  [`Int.toFloatString(){:tact}` in Language→Reference](/language/ref/strings#inttofloatstring)\
  [`Int.toCoinsString(){:tact}` in Language→Reference](/language/ref/strings#inttocoinsstring)

</Callout>

### How to get the current time

Use the `now(){:tact}` method to obtain the current standard [Unix time](https://en.wikipedia.org/wiki/Unix_time).

If you need to store the time in state or encode it in a message, use the following [serialization](/book/integers#serialization): `Int as uint32{:tact}`.

```tact
let currentTime: Int = now();

if (currentTime > 1672080143) {
    // do something
}
```

<Callout>

  **Useful links:**\
  [`now(){:tact}` in Language→Reference](/language/ref/common#now)\
  ["Current Time" in Tact-By-Example](https://tact-by-example.org/04-current-time)

</Callout>

### How to generate a random number

```tact
// Declare a variable to store the random number
let number: Int;

// Generate a new random number, which is an unsigned 256-bit integer
number = randomInt();

// Generate a random number between 1 and 12
number = random(1, 12);
```

<Callout>

  **Useful links:**\
  [`randomInt(){:tact}` in Language→Reference](/language/ref/random#randomInt)\
  [`random(){:tact}` in Language→Reference](/language/ref/random#random)

</Callout>

### How to throw errors

The `throw(){:tact}` function in a contract is useful when we don't know how often to perform a specific action.

It allows intentional exception or error handling, which leads to the termination of the current transaction and reverts any state changes made during that transaction.

```tact
let number: Int = 198;

// the error will be triggered anyway
throw(36);

// the error will be triggered only if the number is greater than 50
nativeThrowWhen(35, number > 50);

// the error will be triggered only if the number is NOT EQUAL to 198
nativeThrowUnless(39, number == 198);
```

<Callout>

  **Useful links:**\
  [`throw(){:tact}` in Language→Reference](/language/ref/advanced#throw)\
  [Errors in Tact-By-Example](https://tact-by-example.org/03-errors)

</Callout>

### How to send a message with a long text comment

If we need to send a message with a lengthy text comment, we should create a [`String{:tact}`](/book/types#primitive-types) that consists of more than $127$ characters. To do this, we can utilize the [`StringBuilder{:tact}`](/book/types#primitive-types) primitive type and its methods called `beginComment(){:tact}` and `append(){:tact}`. Prior to sending, we should convert this string into a cell using the `toCell(){:tact}` method.

```tact
let comment: StringBuilder = beginComment();
let longString = "..."; // Some string with more than 127 characters.
str_builder.append(longString);

send(SendParameters{
    to: ctx.sender,
    value: 0,
    mode: SendIgnoreErrors,
    body: longString.toCell(),
    bounce: true,
});
```

<Callout>

  **Useful links:**\
  ["Sending messages" in the Book](/book/send#send-message)\
  [`StringBuilder{:tact}` in the Book](/book/types#primitive-types)\
  [`Cell{:tact}` in Language→Reference](/language/ref/cells)

</Callout>

### How to calculate the NFT item `Address` by its index

For Tact's example, you should have the Tact code of the NFT item contract, placed in the same file. You can use the function as you wish, both inside and outside of a contract.

For FunC's example, you should have the code of item contract as a [`Cell{:tact}`](/book/types#primitive-types). The function can be used outside of a contract by changing `self.nftItemCode{:tact}` with a preassigned `nftItemCode`.

Tact:

```tact

get fun getNftItemInit(item_index: Int): StateInit {
// Arguments for NftItem may vary, depending on contract
    return initOf NftItem(collectionAddress, item_index, self.owner_address, self.collection_content);
}

let itemIndex: Int = 0; // put your index
let itemInit: StateInit = self.getNftItemInit(itemIndex);
let itemAddress: Address = contractAddress(nft_init);
```

FunC (may also vary depending on collection's `deploy_item(){:tact}` function):

```func
fun getNftItemInit(item_index: Int): StateInit {
    let data: Cell = beginCell().storeUint(item_index,64).storeSlice(self.nFTContractAddress.asSlice()).endCell();
    let itemInit: StateInit = StateInit{
        data: data,
        code: self.nftItemCode
    };
    return itemInit;
}

let itemIndex: Int = 0; // put your index
let itemAddress: Address = contractAddress(self.getNftItemInit(itemIndex));
```

<Callout>

  **Useful links:**\
  [`initOf{:tact}` in the Book](/book/expressions#initof)\
  [`contractAddress(){:tact}` in Language→Reference](/language/ref/common#contractaddress)\
  [Tact collection and item contracts example](https://github.com/howardpen9/nft-template-in-tact/blob/tutorial/sources/contract.tact)\
  [FunC collection and item contracts example](https://github.com/Cosmodude/TAP/tree/main/contracts)

</Callout>

## Receiving messages

### How to check sender privileges using Ownable trait

```tact
// Ownable has to be imported from stdlib, as well as Deployable, for convenience:
import "@stdlib/ownable";
import "@stdlib/deploy";

message FooBarMsg {
    newVal: Int as uint32;
}

// Ownable trait can limit certain actions to the owner only
contract SenderChecker with Deployable, Ownable {
    owner: Address;     // Ownable trait requires you to add this exact state variable
    val: Int as uint32; // some value

    init() {
        // we can initialize owner to any value we want, the deployer in this case:
        self.owner = sender();
        self.val = 0;
    }

    receive("inc") {
        require(self.owner == sender(), "Only the owner can increase the value!");
        self.val += 1;
    }

    receive(msg: FooBarMsg) {
        require(self.owner == sender(), "Only the owner can set the value!");
        self.val = msg.newVal;
    }
}
```
